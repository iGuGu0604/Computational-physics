# 计算物理第九次作业

高兴 2019302020233 物理科学与技术学院

## 题目

利用二阶贝塞尔曲线绘制闭合曲线

## 效果图

![](C:\Users\28153\Desktop\计算物理\第八次作业\littlebird.png)

## 代码

```
#lang racket
(require 2htdp/image)
(require test-engine/racket-tests)
(require 2htdp/universe)
(require racket/list)
; 利用二阶贝塞尔曲线绘制闭合曲线

(define-struct posn [x y])
; 点的坐标

(define dt 0.005)

; posn-between: posn posn Number -> posn
; 由p1和p2两个点根据比例产生中间的点
; p1是第一个点,p2是第二个点
; ratio-of-distance是中间点到p1的距离比上p1到p2的距离
(define (posn-between p1 p2 ratio-of-distance)
  (let ([x1 (posn-x p1)]
        [y1 (posn-y p1)]
        [x2 (posn-x p2)]
        [y2 (posn-y p2)]
        [rd ratio-of-distance])
    (make-posn (+ x1 (* rd (- x2 x1)))
               (+ y1 (* rd (- y2 y1))))))

; next-level-control-points: [List-of posn] Number -> [List-of posn]
; 由当前的控制点control-points和比例产生下一层的控制点
; 见Bezier curve示意图
(define (next-level-controls-points control-points current-ratio)
  (cond
    [(empty? (rest control-points)) '()]
    [else (cons (posn-between (first control-points) (second control-points) current-ratio)
           (next-level-controls-points (rest control-points) current-ratio))]))


; foldr-two: [List-of X] Func -> [List-of (Func x1 x2)]
; 依次计算相邻两个元素的Func值,结果保存在新列表中
; Func需要两个输入参数
(define (foldr-two a-list f)
  (let _ ([remaining-list a-list]
          [acc '()])
    (if (empty? (rest remaining-list))
        (reverse acc)
        (_ (rest remaining-list) (cons (f (first remaining-list) (second remaining-list)) acc)))))

(define (next-level-control-points.v2 control-points current-ratio)
  (foldr-two control-points
             (lambda (a b) (posn-between a b current-ratio))))

; move-point: [List-of posn] Number -> posn
; 按照ratio由控制点得到移动点
(define (move-point control-points current-ratio)
  (cond
    [(empty? (rest control-points)) (first control-points)]
    [else (move-point (next-level-controls-points control-points current-ratio) current-ratio)]))

; fold-reduction: [List-of X] Func -> Value
; 迭代至最后只剩下一个结果
(define (fold-reduction a-list f)
  (let _ ([remaining-list a-list])
    (if (empty? (rest remaining-list))
        (first remaining-list)
        (_ (f remaining-list)))))

(define (move-point.v2 control-points current-ratio)
  (fold-reduction control-points
                  (lambda (a) (next-level-control-points.v2 a current-ratio))))

; get-bezier-points: [List-of posn] Number -> [List-of Posn]
; 得到bezier曲线
(define (get-bezier-points control-points dt)
  (for/list ([t (in-range 0 1.0 dt)]) (move-point.v2 control-points t)))

(define-struct border [x y dx/dt dy/dt])

(define (2nd-bezier-curve-point-with-tangent control-points t)
  (let ([x0 (posn-x (first control-points))]
        [y0 (posn-y (first control-points))]
        [x1 (posn-x (second control-points))]
        [y1 (posn-y (second control-points))]
        [x2 (posn-x (third control-points))]
        [y2 (posn-y (third control-points))])
    (let-values
        ([(x y)
          (values (+ (* (sqr (- 1 t)) x0) (* 2 t (- 1 t) x1) (* t t x2))
                  (+ (* (sqr (- 1 t)) y0) (* 2 t (- 1 t) y1) (* t t y2)))]
         [(dx/dt dy/dt)
          (normalize-vector (+ (* -2 (- 1 t) x0) (* (- 2 (* 4 t)) x1) (* 2 t x2))
                            (+ (* -2 (- 1 t) y0) (* (- 2 (* 4 t)) y1) (* 2 t y2)))])
      (make-border x y dx/dt dy/dt))))

(define (normalize-vector x y)
  (let ([l (sqrt (+ (sqr x) (sqr y)))])
    (values (/ x l) (/ y l))))

(define (get-2nd-bezier-points-with-tangent control-points dt)
  (for/list ([t (in-range 0 1.0 dt)]) (2nd-bezier-curve-point-with-tangent control-points t)))

(define (first-three posn-list)
  (cons (first posn-list) (cons (second posn-list) (cons (third posn-list) '()))))

(define (get-all-2nd-bezier-points-with-tangent control-points dt)
  (foldr-three control-points
               (lambda (x) (get-2nd-bezier-points-with-tangent (first-three x) dt))))

; foldr-theree: [List-of X] Func -> [List-of (Func x1 x2 x3)]
; 依次计算相邻三个元素的Func值,结果保存在新列表中
; Func需要三个输入参数
; 迭代过程依次跳过两个元素
(define (foldr-three a-list f)
  (let _ ([remaining-list a-list]
          [acc '()])
    (if (or (empty? (rest remaining-list)) (empty? (rest (rest remaining-list))))
        (flatten (reverse acc))
        (_ (rest (rest remaining-list))
           (cons (f (first-three remaining-list)) acc)))))

; create-control-points: [List-of [List-of Number]] -> [List-of posn]
; 由数字列表得到控制点列表
(define (create-control-points posn-list)
  (map (lambda (a) (make-posn (first a) (second a))) posn-list))

;; color -> image
;; create a dot of the given color
(define (dot color) (square 3 'solid color))

(define BG (rectangle 600 600 'solid 'white))

; draw-points-with-tangent: [List-of border] IMG syntax -> IMG
; 将点绘制在图片上
(define (draw-points-with-tangent point-list background color)
  (foldl (lambda (a b) (place-image (dot color) (border-x a) (border-y a) b)) background point-list))

(define body (create-control-points '((300 100) (450 100) (450 250) (450 300) (500 300) (500 500) (300 500) (100 500) (100 300) (100 100) (300 100))))
(define beak (create-control-points '((113 200)(-30 215)(108 230))))
(define wing (create-control-points '((300 300)(400 310)(400 400)(310 400) (300 300))))
(define bird-body (draw-points-with-tangent (get-all-2nd-bezier-points-with-tangent body dt) BG 'yellow))
(define bird-beak (draw-points-with-tangent (get-all-2nd-bezier-points-with-tangent beak dt) bird-body 'yellow))
(define bird-wing (draw-points-with-tangent (get-all-2nd-bezier-points-with-tangent wing dt) bird-beak 'yellow))
```

